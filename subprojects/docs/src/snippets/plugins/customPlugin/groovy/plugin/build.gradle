plugins {
    id 'groovy'
    id 'maven-publish'
}

// tag::gradle-api-dependencies[]
// tag::local-groovy-dependencies[]
dependencies {
// end::local-groovy-dependencies[]
    implementation gradleApi()
// end::gradle-api-dependencies[]
// tag::local-groovy-dependencies[]
    implementation localGroovy()
// tag::gradle-api-dependencies[]
}
// end::gradle-api-dependencies[]
// end::local-groovy-dependencies[]

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

publishing {
    repositories {
        maven {
            url = uri(layout.buildDirectory.dir("repo"))
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

// Needed when using ProjectBuilder
class AddOpensArgProvider implements CommandLineArgumentProvider {
    private final Test test;
    public AddOpensArgProvider(Test test) {
        this.test = test;
    }
    @Override
    Iterable<String> asArguments() {
        return test.javaVersion.isCompatibleWith(JavaVersion.VERSION_1_9)
            ? ["--add-opens=java.base/java.lang=ALL-UNNAMED"]
            : []
    }
}
tasks.withType(Test).configureEach {
    jvmArgumentProviders.add(new AddOpensArgProvider(it))
}
