plugins {
    id 'base'
}

// Fake NPM task that would normally execute npm with its provided arguments
abstract class NpmTask extends DefaultTask {

    ListProperty<String> args = project.objects.listProperty(String)

    @TaskAction
    void run() {
        project.file("$project.buildDir/bundle.js").withOutputStream { stream ->
            project.file("scripts").listFiles().sort().each {
                stream.write(it.bytes)
            }
        }
    }
}

// tag::bundle-task[]
@CacheableTask                                       // <1>
abstract class BundleTask extends NpmTask {

    @Override @Internal                              // <2>
    ListProperty<String> getArgs() {
        super.getArgs()
    }

    @InputDirectory
    @SkipWhenEmpty
    @PathSensitive(PathSensitivity.RELATIVE)         // <3>
    abstract DirectoryProperty getScripts()

    @InputFiles
    @PathSensitive(PathSensitivity.RELATIVE)         // <4>
    abstract ConfigurableFileCollection getConfigFiles()

    @OutputFile
    abstract RegularFileProperty getBundle()

    BundleTask() {
        args.addAll("run", "bundle")
        bundle.set(project.layout.buildDirectory.file("bundle.js"))
        scripts.set(project.layout.projectDirectory.dir("scripts"))
        configFiles.from(project.layout.projectDirectory.file("package.json"))
        configFiles.from(project.layout.projectDirectory.file("package-lock.json"))
    }
}

tasks.register('bundle', BundleTask)
// end::bundle-task[]

tasks.register('printBundle') {
    dependsOn bundle
    doLast {
        println project.layout.buildDirectory.file("bundle.js").get().asFile.text
    }
}
