repositories {
    mavenCentral()
}

configurations {
    myPlugin {
        canBeConsumed = false
        canBeResolved = false
    }
    myPluginClasspath {
        extendsFrom(myPlugin)
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    myPlugin "org.apache.commons:commons-lang3:3.3.1"
}

// tag::disabling-one-configuration[]
configurations {
    myPluginClasspath {
        resolutionStrategy {
            disableDependencyVerification()
        }
    }
}
// end::disabling-one-configuration[]

tasks.register("checkDependencies") {
    inputs.files(configurations.myPluginClasspath)
    doLast {
        println(configurations.myPluginClasspath.files)
    }
}

// tag::disabling-detached-configuration[]
tasks.register("checkDetachedDependencies") {
    doLast {
        def detachedConf = configurations.detachedConfiguration(dependencies.create("org.apache.commons:commons-lang3:3.3.1"))
        detachedConf.resolutionStrategy.disableDependencyVerification()
        println(detachedConf.files)
    }
}
// end::disabling-detached-configuration[]
